LittleEndian();
DisplayFormatHex();
/* BitfieldDisablePadding(); */

local uint32 offset, i, descriptor_start, descriptor_count, descriptor, index;
local ubyte  current_page;

enum <uint32> EntryNumber {
	EntryNumber_ThreadInfo        = 3,
	EntryNumber_EnableSystemCalls = 4,
	EntryNumber_MemoryMap         = 6,
	EntryNumber_IoMemoryMap       = 7,
	EntryNumber_MemoryRegionMap   = 10,
	EntryNumber_EnableInterrupts  = 11,
	EntryNumber_MiscParams        = 13,
	EntryNumber_KernelVersion     = 14,
	EntryNumber_HandleTableSize   = 15,
	EntryNumber_MiscFlags         = 16
};

local EntryNumber entry_number;

enum <ubyte> ProcessAddressSpace {
	ProcessAddressSpace_AddressSpace32Bit           = 0,
	ProcessAddressSpace_AddressSpace64BitOld        = 1,
	ProcessAddressSpace_AddressSpace32BitNoReserved = 2,
	ProcessAddressSpace_AddressSpace64Bit           = 3
};

struct MetaFlags {
	ubyte MetaFlags_Is64BitInstruction       : 1;
	ProcessAddressSpace MetaFlags_ProcessAddressSpace : 3;
	ubyte MetaFlags_OptimizeMemoryAllocation : 1;
	ubyte MetaFlags_Reserved1                : 3;
};

struct Version {
	uint32 Version_MinorRelstep : 8;
	uint32 Version_MajorRelstep : 8;
	uint32 Version_Micro        : 4;
	uint32 Version_Minor        : 6;
	uint32 Version_Major        : 6;
};

struct SectionHeader {
	uint32 offset;
	uint32 size;
};

struct MetaHeader {
	char   magic[0x4];
	ubyte  signature_key_generation; /* acid_header.signature */
	ubyte  reserved_1[0x7];
	MetaFlags flags;
	ubyte  reserved_2;
	ubyte  main_thread_priority;
	ubyte  main_thread_core_number;
	uint32 reserved_3;
	uint32 system_resource_size;
	Version version;
	uint32 main_thread_stack_size;
	char   name[0x10];
	char   product_code[0x10];
	ubyte  reserved_4[0x30];
	SectionHeader aci_section_header;
	SectionHeader acid_section_header;
} meta_header;

enum <uint32> MemoryRegion {
	MemoryRegion_Application     = 0,
	MemoryRegion_Applet          = 1,
	MemoryRegion_SecureSystem    = 2,
	MemoryRegion_NonSecureSystem = 3
};

struct AcidFlags {
	uint32 AcidFlags_Production          : 1;
	uint32 AcidFlags_UnqualifiedApproval : 1;
	MemoryRegion AcidFlags_MemoryRegion  : 2;
	uint32 AcidFlags_Reserved1           : 28;
};

struct FlagPresetsBit {
	uint64 FlagPresetsBit_ApplicationInfo             : 1;
	uint64 FlagPresetsBit_BootModeControl             : 1;
	uint64 FlagPresetsBit_Calibration                 : 1;
	uint64 FlagPresetsBit_SystemSaveData              : 1;
	uint64 FlagPresetsBit_GameCard                    : 1;
	uint64 FlagPresetsBit_SaveDataBackUp              : 1;
	uint64 FlagPresetsBit_SaveDataManagement          : 1;
	uint64 FlagPresetsBit_BisAllRaw                   : 1;
	uint64 FlagPresetsBit_GameCardRaw                 : 1;
	uint64 FlagPresetsBit_GameCardPrivate             : 1;
	uint64 FlagPresetsBit_SetTime                     : 1;
	uint64 FlagPresetsBit_ContentManager              : 1;
	uint64 FlagPresetsBit_ImageManager                : 1;
	uint64 FlagPresetsBit_CreateSaveData              : 1;
	uint64 FlagPresetsBit_SystemSaveDataManagement    : 1;
	uint64 FlagPresetsBit_BisFileSystem               : 1;
	uint64 FlagPresetsBit_SystemUpdate                : 1;
	uint64 FlagPresetsBit_SaveDataMeta                : 1;
	uint64 FlagPresetsBit_DeviceSaveData              : 1;
	uint64 FlagPresetsBit_SettingsControl             : 1;
	uint64 FlagPresetsBit_SystemData                  : 1;
	uint64 FlagPresetsBit_SdCard                      : 1;
	uint64 FlagPresetsBit_Host                        : 1;
	uint64 FlagPresetsBit_FillBis                     : 1;
	uint64 FlagPresetsBit_CorruptSaveData             : 1;
	uint64 FlagPresetsBit_SaveDataForDebug            : 1;
	uint64 FlagPresetsBit_FormatSdCard                : 1;
	uint64 FlagPresetsBit_GetRightsId                 : 1;
	uint64 FlagPresetsBit_RegisterExternalKey         : 1;
	uint64 FlagPresetsBit_RegisterUpdatePartition     : 1;
	uint64 FlagPresetsBit_SaveDataTransfer            : 1;
	uint64 FlagPresetsBit_DeviceDetection             : 1;
	uint64 FlagPresetsBit_AccessFailureResolution     : 1;
	uint64 FlagPresetsBit_SaveDataTransferVersion2    : 1;
	uint64 FlagPresetsBit_RegisterProgramIndexMapInfo : 1;
	uint64 FlagPresetsBit_CreateOwnSaveData           : 1;
	uint64 FlagPresetsBit_Unknown1                    : 1;  /* Name unknown. */
	uint64 FlagPresetsBit_Reserved1                   : 25;
	uint64 FlagPresetsBit_Debug                       : 1;
	uint64 FlagPresetsBit_FullPermission              : 1;
};

struct FacDescriptor {
	ubyte  version;
	ubyte  content_owner_id_count;
	ubyte  save_data_owner_id_count;
	ubyte  reserved_1;
	FlagPresetsBit flag_presets_bit;
	uint64 content_owner_id_min;
	uint64 content_owner_id_max;
	uint64 save_data_owner_id_min;
	uint64 save_data_owner_id_max;
	uint64 content_owner_ids[content_owner_id_count];
	uint64 save_data_owner_ids[save_data_owner_id_count];
};

struct Descriptor {
	ubyte Descriptor_NameLength : 3;
	ubyte Descriptor_Reserved1  : 4;
	ubyte Descriptor_IsServer   : 1;
};

struct SacEntry {
	Descriptor descriptor;
	char name[descriptor.Descriptor_NameLength + 1];
};

struct SacData {
	while (FTell() < (offset + sac_section_header.offset + sac_section_header.size)) {
		SacEntry sac_entry;
	}
};

struct ThreadInfo {
	uint32 entry_number     : 3;
	uint32 reserved_1       : 1;
	uint32 lowest_priority  : 6;
	uint32 highest_priority : 6;
	uint32 min_core_number  : 8;
	uint32 max_core_number  : 8;
};

struct SystemCallIds {
	/* Workaround for BitfieldDisablePadding() bug. It's not pretty and causes the size in the variables tab to show 0. */
	FSkip(-0x4);
	uint32 : 5;
	switch (index) {
		case 0:
			uint32 SystemCallId_Unknown1                       : 1;
			uint32 SystemCallId_SetHeapSize                    : 1;
			uint32 SystemCallId_SetMemoryPermission            : 1;
			uint32 SystemCallId_SetMemoryAttribute             : 1;
			uint32 SystemCallId_MapMemory                      : 1;
			uint32 SystemCallId_UnmapMemory                    : 1;
			uint32 SystemCallId_QueryMemory                    : 1;
			uint32 SystemCallId_ExitProcess                    : 1;
			uint32 SystemCallId_CreateThread                   : 1;
			uint32 SystemCallId_StartThread                    : 1;
			uint32 SystemCallId_ExitThread                     : 1;
			uint32 SystemCallId_SleepThread                    : 1;
			uint32 SystemCallId_GetThreadPriority              : 1;
			uint32 SystemCallId_SetThreadPriority              : 1;
			uint32 SystemCallId_GetThreadCoreMask              : 1;
			uint32 SystemCallId_SetThreadCoreMask              : 1;
			uint32 SystemCallId_GetCurrentProcessorNumber      : 1;
			uint32 SystemCallId_SignalEvent                    : 1;
			uint32 SystemCallId_ClearEvent                     : 1;
			uint32 SystemCallId_MapSharedMemory                : 1;
			uint32 SystemCallId_UnmapSharedMemory              : 1;
			uint32 SystemCallId_CreateTransferMemory           : 1;
			uint32 SystemCallId_CloseHandle                    : 1;
			uint32 SystemCallId_ResetSignal                    : 1;
			break;
		case 1:
			uint32 SystemCallId_WaitSynchronization            : 1;
			uint32 SystemCallId_CancelSynchronization          : 1;
			uint32 SystemCallId_ArbitrateLock                  : 1;
			uint32 SystemCallId_ArbitrateUnlock                : 1;
			uint32 SystemCallId_WaitProcessWideKeyAtomic       : 1;
			uint32 SystemCallId_SignalProcessWideKey           : 1;
			uint32 SystemCallId_GetSystemTick                  : 1;
			uint32 SystemCallId_ConnectToNamedPort             : 1;
			uint32 SystemCallId_SendSyncRequestLight           : 1;
			uint32 SystemCallId_SendSyncRequest                : 1;
			uint32 SystemCallId_SendSyncRequestWithUserBuffer  : 1;
			uint32 SystemCallId_SendAsyncRequestWithUserBuffer : 1;
			uint32 SystemCallId_GetProcessId                   : 1;
			uint32 SystemCallId_GetThreadId                    : 1;
			uint32 SystemCallId_Break                          : 1;
			uint32 SystemCallId_OutputDebugString              : 1;
			uint32 SystemCallId_ReturnFromException            : 1;
			uint32 SystemCallId_GetInfo                        : 1;
			uint32 SystemCallId_FlushEntireDataCache           : 1;
			uint32 SystemCallId_FlushDataCache                 : 1;
			uint32 SystemCallId_MapPhysicalMemory              : 1;
			uint32 SystemCallId_UnmapPhysicalMemory            : 1;
			uint32 SystemCallId_GetDebugFutureThreadInfo       : 1;
			uint32 SystemCallId_GetLastThreadInfo              : 1;
			break;
		case 2:
			uint32 SystemCallId_GetResourceLimitLimitValue     : 1;
			uint32 SystemCallId_GetResourceLimitCurrentValue   : 1;
			uint32 SystemCallId_SetThreadActivity              : 1;
			uint32 SystemCallId_GetThreadContext3              : 1;
			uint32 SystemCallId_WaitForAddress                 : 1;
			uint32 SystemCallId_SignalToAddress                : 1;
			uint32 SystemCallId_SynchronizePreemptionState     : 1;
			uint32 SystemCallId_Unknown2                       : 1;
			uint32 SystemCallId_Unknown3                       : 1;
			uint32 SystemCallId_Unknown4                       : 1;
			uint32 SystemCallId_Unknown5                       : 1;
			uint32 SystemCallId_Unknown6                       : 1;
			uint32 SystemCallId_KernelDebug                    : 1;
			uint32 SystemCallId_ChangeKernelTraceState         : 1;
			uint32 SystemCallId_Unknown7                       : 1;
			uint32 SystemCallId_Unknown8                       : 1;
			uint32 SystemCallId_CreateSession                  : 1;
			uint32 SystemCallId_AcceptSession                  : 1;
			uint32 SystemCallId_ReplyAndReceiveLight           : 1;
			uint32 SystemCallId_ReplyAndReceive                : 1;
			uint32 SystemCallId_ReplyAndReceiveWithUserBuffer  : 1;
			uint32 SystemCallId_CreateEvent                    : 1;
			uint32 SystemCallId_Unknown9                       : 1;
			uint32 SystemCallId_Unknown10                      : 1;
			break;
		case 3:
			uint32 SystemCallId_MapPhysicalMemoryUnsafe        : 1;
			uint32 SystemCallId_UnmapPhysicalMemoryUnsafe      : 1;
			uint32 SystemCallId_SetUnsafeLimit                 : 1;
			uint32 SystemCallId_CreateCodeMemory               : 1;
			uint32 SystemCallId_ControlCodeMemory              : 1;
			uint32 SystemCallId_SleepSystem                    : 1;
			uint32 SystemCallId_ReadWriteRegister              : 1;
			uint32 SystemCallId_SetProcessActivity             : 1;
			uint32 SystemCallId_CreateSharedMemory             : 1;
			uint32 SystemCallId_MapTransferMemory              : 1;
			uint32 SystemCallId_UnmapTransferMemory            : 1;
			uint32 SystemCallId_CreateInterruptEvent           : 1;
			uint32 SystemCallId_QueryPhysicalAddress           : 1;
			uint32 SystemCallId_QueryIoMapping                 : 1;
			uint32 SystemCallId_CreateDeviceAddressSpace       : 1;
			uint32 SystemCallId_AttachDeviceAddressSpace       : 1;
			uint32 SystemCallId_DetachDeviceAddressSpace       : 1;
			uint32 SystemCallId_MapDeviceAddressSpaceByForce   : 1;
			uint32 SystemCallId_MapDeviceAddressSpaceAligned   : 1;
			uint32 SystemCallId_MapDeviceAddressSpace          : 1;
			uint32 SystemCallId_UnmapDeviceAddressSpace        : 1;
			uint32 SystemCallId_InvalidateProcessDataCache     : 1;
			uint32 SystemCallId_StoreProcessDataCache          : 1;
			uint32 SystemCallId_FlushProcessDataCache          : 1;
			break;
		case 4:
			uint32 SystemCallId_DebugActiveProcess             : 1;
			uint32 SystemCallId_BreakDebugProcess              : 1;
			uint32 SystemCallId_TerminateDebugProcess          : 1;
			uint32 SystemCallId_GetDebugEvent                  : 1;
			uint32 SystemCallId_ContinueDebugEvent             : 1;
			uint32 SystemCallId_GetProcessList                 : 1;
			uint32 SystemCallId_GetThreadList                  : 1;
			uint32 SystemCallId_GetDebugThreadContext          : 1;
			uint32 SystemCallId_SetDebugThreadContext          : 1;
			uint32 SystemCallId_QueryDebugProcessMemory        : 1;
			uint32 SystemCallId_ReadDebugProcessMemory         : 1;
			uint32 SystemCallId_WriteDebugProcessMemory        : 1;
			uint32 SystemCallId_SetHardwareBreakPoint          : 1;
			uint32 SystemCallId_GetDebugThreadParam            : 1;
			uint32 SystemCallId_Unknown11                      : 1;
			uint32 SystemCallId_GetSystemInfo                  : 1;
			uint32 SystemCallId_CreatePort                     : 1;
			uint32 SystemCallId_ManageNamedPort                : 1;
			uint32 SystemCallId_ConnectToPort                  : 1;
			uint32 SystemCallId_SetProcessMemoryPermission     : 1;
			uint32 SystemCallId_MapProcessMemory               : 1;
			uint32 SystemCallId_UnmapProcessMemory             : 1;
			uint32 SystemCallId_QueryProcessMemory             : 1;
			uint32 SystemCallId_MapProcessCodeMemory           : 1;
			break;
		case 5:
			uint32 SystemCallId_UnmapProcessCodeMemory         : 1;
			uint32 SystemCallId_CreateProcess                  : 1;
			uint32 SystemCallId_StartProcess                   : 1;
			uint32 SystemCallId_TerminateProcess               : 1;
			uint32 SystemCallId_GetProcessInfo                 : 1;
			uint32 SystemCallId_CreateResourceLimit            : 1;
			uint32 SystemCallId_SetResourceLimitLimitValue     : 1;
			uint32 SystemCallId_CallSecureMonitor              : 1;
			uint32 SystemCallId_Reserved1                      : 16;
			break;
	}
};

struct EnableSystemCalls {
	uint32 entry_number : 4;
	uint32 reserved_1   : 1;
	SystemCallIds system_call_ids;
	uint32 index        : 3;
};

enum <uint32> PermissionType {
	PermissionType_RW = 0,
	PermissionType_RO = 1
};

enum <uint32> MapType {
	MapType_Io     = 0,
	MapType_Static = 1
};

struct MemoryMap {
	uint32 entry_number : 6;
	uint32 reserved_1   : 1;
	if (!current_page) {
		uint32 begin_address : 24; /* begin_address << 12 */
		PermissionType permission_type : 1;
		current_page = 1;
	} else {
		uint32 size          : 20; /* size << 12 */
		uint32 reserved_2    : 4;
		MapType map_type     : 1;
		current_page = 0;
	}
};

struct IoMemoryMap {
	uint32 entry_number  : 7;
	uint32 reserved_1    : 1;
	uint32 begin_address : 24; /* begin_address << 12 */
};

enum <uint32> RegionType {
	RegionType_NoMapping         = 0,
	RegionType_KernelTraceBuffer = 1,
	RegionType_OnMemoryBootImage = 2,
	RegionType_DTB               = 3
};

struct MemoryRegionMap {
	uint32 entry_number : 10;
	uint32 reserved_1   : 1;
	RegionType region_type_1 : 6;
	PermissionType permission_type_1 : 1;
	RegionType region_type_2 : 6;
	PermissionType permission_type_2 : 1;
	RegionType region_type_3 : 6;
	PermissionType permission_type_3 : 1;
};

struct EnableInterrupts {
	uint32 entry_number       : 11;
	uint32 reserved_1         : 1;
	uint32 interrupt_number_1 : 10;
	uint32 interrupt_number_2 : 10;
};

enum <uint32> ProgramType {
	ProgramType_System      = 0,
	ProgramType_Application = 1,
	ProgramType_Applet      = 2
};

struct MiscParams {
	uint32 entry_number : 13;
	uint32 reserved_1   : 1;
	ProgramType program_type : 3;
	uint32 reserved_2   : 15;
};

struct KernelVersion {
	uint32 entry_number  : 14;
	uint32 reserved_1    : 1;
	uint32 minor_version : 4;
	uint32 major_version : 13;
};

struct HandleTableSize {
	uint32 entry_number      : 15;
	uint32 reserved_1        : 1;
	uint32 handle_table_size : 10;
	uint32 reserved_2        : 6;
};

struct MiscFlags {
	uint32 entry_number : 16;
	uint32 reserved_1   : 1;
	uint32 enable_debug : 1;
	uint32 force_debug  : 1;
	uint32 reserved_2   : 13;
};

struct KcData {
	descriptor_start = FTell();
	descriptor_count = kc_section_header.size / 0x4;
	for (i = 0; i < descriptor_count; i++) {
		descriptor = ReadUInt(descriptor_start + (i * 0x4));
		entry_number = 0;
		while (descriptor & 0x1) {
			descriptor >>= 1;
			entry_number++;
		}
		switch (entry_number) {
			case EntryNumber_ThreadInfo:
				ThreadInfo thread_info;
				break;
			case EntryNumber_EnableSystemCalls:
				index = (descriptor >> 25) & 0x7;
				EnableSystemCalls enable_system_calls;
				break;
			case EntryNumber_MemoryMap:
				MemoryMap memory_map;
				break;
			case EntryNumber_IoMemoryMap:
				IoMemoryMap io_memory_map;
				break;
			case EntryNumber_MemoryRegionMap:
				MemoryRegionMap memory_region_map;
				break;
			case EntryNumber_EnableInterrupts:
				EnableInterrupts enable_interrupts;
				break;
			case EntryNumber_MiscParams:
				MiscParams misc_params;
				break;
			case EntryNumber_KernelVersion:
				KernelVersion kernel_version;
				break;
			case EntryNumber_HandleTableSize:
				HandleTableSize handle_table_size;
				break;
			case EntryNumber_MiscFlags:
				MiscFlags misc_flags;
				break;
		}
	}
};

FSeek(meta_header.acid_section_header.offset);
offset = meta_header.acid_section_header.offset;
struct AcidHeader {
	ubyte  signature[0x100];
	ubyte  public_key[0x100];
	char   magic[0x4];
	uint32 size;
	uint32 reserved_1;
	AcidFlags flags;
	uint64 program_id_min;
	uint64 program_id_max;
	SectionHeader fac_section_header;
	SectionHeader sac_section_header;
	SectionHeader kc_section_header;
	uint64 reserved_2;
	FSeek(meta_header.acid_section_header.offset + fac_section_header.offset);
	FacDescriptor fac_descriptor;
	FSeek(meta_header.acid_section_header.offset + sac_section_header.offset);
	SacData sac_data;
	FSeek(meta_header.acid_section_header.offset + kc_section_header.offset);
	KcData kc_data;
} acid_header;

enum <ubyte> Accessibility {
	Accessibility_Read      = 1,
	Accessibility_Write     = 2,
	Accessibility_ReadWrite = 3
};

uint32 AlignUp(uint32 offset, uint32 alignment) {
	local uint32 mask = alignment - 1;
	return (mask + offset) & ~mask;
}

struct FacData {
	ubyte  version;
	ubyte  reserved_1[0x3];
	FlagPresetsBit flag_presets_bit;
	uint32 content_owner_info_offset;
	uint32 content_owner_info_size;
	uint32 save_data_owner_info_offset;
	uint32 save_data_owner_info_size;
	if (content_owner_info_size != 0) {
		uint32 content_owner_id_count;
		uint64 content_owner_ids[content_owner_id_count];
	}
	if (save_data_owner_info_size != 0) {
		uint32 save_data_owner_id_count;
		Accessibility accessibilities[save_data_owner_id_count];
		local uint32 target = startof(accessibilities) + (save_data_owner_id_count * 0x1);
		ubyte  reserved_2[AlignUp(target, 0x4) - target];
		uint64 save_data_owner_ids[save_data_owner_id_count];
	}
};

FSeek(meta_header.aci_section_header.offset);
offset = meta_header.aci_section_header.offset;
struct AciHeader {
	char   magic[0x4];
	ubyte  reserved_1[0xC];
	uint64 program_id;
	uint64 reserved_2;
	SectionHeader fac_section_header;
	SectionHeader sac_section_header;
	SectionHeader kc_section_header;
	uint64 reserved_3;
	FSeek(meta_header.aci_section_header.offset + fac_section_header.offset);
	FacData fac_data;
	FSeek(meta_header.aci_section_header.offset + sac_section_header.offset);
	SacData sac_data;
	FSeek(meta_header.aci_section_header.offset + kc_section_header.offset);
	KcData kc_data;
} aci_header;
